ext {
  grailsVersion = "3.0.0.BUILD-SNAPSHOT"
}

buildscript {
    ext {
      grailsVersion = "3.0.0.BUILD-SNAPSHOT"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.grails.org/grails/core" }
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

version "2.0.0-SNAPSHOT"
group "org.grails.plugins"

apply plugin: "grails-plugin"
apply plugin: "maven"
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'


repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url "http://dl.bintray.com/bertramlabs/asset-pipeline" }
  maven { url "http://repo.grails.org/grails/core" }
}

configurations {
  provided
}

dependencies {
  compile "org.grails:grails-dependencies:$grailsVersion"
  runtime 'org.mozilla:rhino:1.7R4'
  compile 'commons-io:commons-io:2.2'
  compile "com.bertramlabs.plugins:asset-pipeline-core:2.0.2-SNAPSHOT"
  compile 'log4j:log4j:1.2.16'
  testCompile "org.grails:grails-plugin-testing:$grailsVersion"
  provided 'javax.servlet:javax.servlet-api:3.1.0'
}

sourceSets {
    main {
        compileClasspath += files(configurations.provided)
    }
    test {
        compileClasspath += files(configurations.provided)
        runtimeClasspath += files(configurations.provided)
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'asset-pipeline'

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            credentials {
                username project.properties.get('artifactoryPublishUsername')
                password project.properties.get('artifactoryPublishPassword')
            }
            url "https://repo.grails.org/grails/libs-snapshots-local/"
        }
    }
}
